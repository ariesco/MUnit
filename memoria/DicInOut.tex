%!TEX root = tfg_fiesta.tex

A lo largo de este capítulo explicaremos la creación del módulo encargado de la entrada y la salida de datos, y de los diccionarios de figuras, que serán la estructura de datos que utilizaremos para almacenar la información.\par

\section{Diccionarios}

Dando por concluidos los módulos referidos a la geometría en dos dimensiones comenzamos con el de entrada/salida. Queremos que este tenga una interfaz simple para crear las figuras, del tipo: ``la circunferencia c1 con centro el punto p y radio z''. Así pues, necesitaremos crear una estructura de datos que se encargue de almacenar toda la información. Para esto podríamos crear simplemente una lista, como ya hemos hecho otras veces, pero resulta más natural, al constar las figuras de nombre, que hagamos uso directamente de un diccionario. \par

El diccionario deberá contar con unas estructuras internas más sencillas que llamaremos entradas (\texttt{entry}), las cuales llevarán por un lado el nombre, y por otro la figura asociada. Para los tipos de las figuras no tenemos ningun problema, ya creamos el tipo \texttt{Figure} en el módulo \texttt{GEO2D}. Para los nombres por el contrario no tenemos ningún tipo creado por nosotros que nos sirva, así que haremos uso de uno incluido en Core Maude, los \texttt{Qid}. Los \texttt{Qid} son identificadores precedidos de \verb$'$. Con esto ya decidido veamos la creación de los diccionarios:\par

{\codesize
\begin{verbatim}
fmod DICC-FIGURES is
   pr GEO2D .
   pr QID .

   sorts Dic Entry .
   subsorts Entry < Dic .

   op _->_ : Qid Figure -> Entry [ctor] .
   op mtD : -> Dic [ctor] .
   op _._ : Dic Dic -> Dic [ctor assoc comm id: mtD] .

\end{verbatim}
}

Como se puede ver la definición resulta natural, \texttt{mtD} es el diccionario vacío y \verb"_._" la operación que nos permite unirlos, siendo el vacío su elemento neutro. Los \texttt{Entry} se definen uniendo sus dos términos mediante una flecha.\par

A continuación definimos las operaciones de los diccionaros que harán uso de la conmutatividad de la unión para facilitar su implementación. La primera función que crearemos será aquella que dado un diccionario y un \texttt{Qid} nos devuelva la figura correspondiente, seguida de aquella que nos indica recibiendo los mismos datos si el diccionario contiene alguna figura con ese identificador. Estas funciones son:
{\codesize
\begin{verbatim}



   op _[_] : Dic Qid ~> Figure .           op contains? : Dic Qid -> Bool .
   *** Dado un Qid devuelve la figura      *** Dado un Qid indica si el diccionario
   *** correspondiente.                    *** tiene una entreda que le corresponda.
   eq (D . Q -> F)[Q] = F .                eq contains?((Q -> F) . D, Q) = true .
                                           eq contains?(D, Q) = false [owise] .

\end{verbatim}
}

En la primera hemos utilizado \verb"~>", que nos define una función parcial. Continuando con estas operaciones básicas se procederá a crear una que nos permita actualizar el diccionario. Esta función se encargará además de evitar las repeticiones, tanto en los identificadores como en las figuras, y en caso de que la información no estuviese ya en él simplemente la incluye. Esta función queda entonces como sigue:

{\codesize
\begin{verbatim}
   op _[_/_] : Dic Qid Figure -> Dic .
   eq (D . Q -> F)[Q / F'] = D . Q -> F' .
   eq (D . Q -> F)[Q' / F] = D . Q -> F .
   ceq (D . Q -> r1)[Q' / r2] = D . Q -> r1
     if are-equal(r1, r2) .   
   eq D[Q / F] = D . Q -> F [owise] .
\end{verbatim}
}

Como se puede ver presenta un caso diferenciado, que es aquel que se refiere a las rectas, como ya se explicó en el apartado correspondiente. El resto de la función es sencillo; el primer caso cubre una actualización, el segundo, junto con el de las rectas, evita que introduzcamos de nuevo una figura pero con distinto nombre. El último simplemente cubre el caso de que no estuviese y lo añade directamente.\par

Para concluir esta sección crearemos dos funciones, la primera de ellas (\verb"_in_") será análoga a \verb"contains?" pero en este caso recibirá la figura en vez del nombre, la segunda (\verb"name") será análoga a aquella que nos daba una figura dado su nombre. Sin embargo en este caso sí cubrirá el caso en que no esté, pues lo necesitamos a la hora de mostrar la información por pantalla. Para ello haremos uso de un nuevo tipo \verb"QidList", que se corresponde con las listas de \verb"Qid", de él tomaremos un único elemento, \verb"nil", la lista vacía, que usaremos para no devolver nada. Mostramos a continuación estas dos funciones:\par 

{\codesize
\begin{verbatim}
   op _in_ : Figure Dic -> Bool .             op name : Dic Figure -> QidList .
   *** Indica si la figura pertenece al       *** Dado un diccionnario y una figura 
   *** diccionario.                           *** devuelve el identificador de esta.
   eq F in D . (Q -> F) = true .              eq name(D . Q -> F, F) = Q .
   eq F in D = false [owise] .                eq name(D , F) = nil [owise] . 
\end{verbatim}
}

A continuación daremos algunos casos del test de unidad para diccionarios para ver claramente su sintaxis:

{\codesize
\begin{verbatim}
(munit DICC-FIGURES is
   assertEqual('p1 -> p(0.0 0.0) ['p1], p(0.0 0.0))
   assertTrue(contains?(('p1 -> p(0.0 0.0)) . ('p2 -> p(1.0 1.0)), 'p1))
   assertEqual('p1 -> p(0.0 0.0) ['p1 / p(1.0 1.0)], 'p1 -> p(1.0 1.0))
   assertFalse(p(0.0 1.0) in ('p1 -> p(0.0 0.0)) . ('p2 -> p(1.0 1.0)))
   assertEqual(name('p -> p(0.0 0.0), p(0.0 0.0)), 'p)
\end{verbatim}
}


\subsection{Diccionario de figuras}

Simplemente con estos diccionarios podríamos dar por creada la estructura de datos. Sin embargo, aunque son totalmente funcionales, carecen de orden, ya que los puntos, rectas y circunferencias se encuentran totalmente mezclados. Así pues construiremos un nuevo tipo \texttt{DicFigures} que se corresponde con un conjunto de tres diccionarios, estando cada uno de ellos destinado a un tipo de figura concreta. Su definición es:

{\codesize
\begin{verbatim}
   sorts Dic Entry DicFigures .
   subsorts Entry < Dic .

   op [_,_,_] : Dic Dic Dic -> DicFigures [ctor] .
\end{verbatim}
}

Las funciones que definiremos para estos nuevos diccionarios serán las mismas que para los dados anteriormente y utilizarán los mismos comandos para mayor comodidad. Un ejemplo de estas serían las siguientes:\par

{\codesize
\begin{verbatim}
   op _[_] : DicFigures Qid ~> Figure .
   eq [Dp . Q -> p1, Dr, Dc][Q] = p1 .
   eq [Dp, Dr . Q -> r1, Dc][Q] = r1 .
   eq [Dp, Dr, Dc . Q -> c1][Q] = c1 . 
\end{verbatim}
}

Los casos de test serían entonces totalmente análogos, pero al igual que antes incluiremos alguno como ejemplo:

{\codesize
\begin{verbatim}
assertEqual(([mtD, mtD, mtD] ['p1 / p(1.0 1.0)]), [('p1 -> p(1.0 1.0)), mtD, mtD])
assertEqual(([mtD, mtD, mtD] ['r1 / r(p(1.0 1.0) p(0.0 0.0))]),
             [mtD, ('r1 -> r(p(1.0 1.0) p(0.0 0.0))), mtD])
assertEqual(([mtD, mtD, mtD] ['c1 / c(p(1.0 1.0), 1.0)]), 
             [mtD, mtD, ('c1 -> c(p(1.0 1.0), 1.0))])
\end{verbatim}
}

\section{Módulo interactivo}

Una vez terminado el módulo referente a las estructuras de datos comenzaremos con el de entrada/salida. Este se encontrará dividido en cinco módulos distintos, dedicándose cada uno a un aspecto distinto de la aplicación. Para la construcción de estos módulos será necesario importar Full Maude, que importaremos al comienzo del archivo. Además, necesitaremos importar la signatura de Full Maude dentro del módulo \verb"FULL-MAUDE-SIGN". Como vamos a añadir nuevos comandos lo importamos con el comando \verb"inc". \par

En este primer módulo crearemos los comandos que más adelante utilizaremos para la entrada de datos. Para ello usaremos los sorts \texttt{@Token@} y \texttt{@Bubble@}, predefinidos en Full Maude. El primero indica que se espera un único token, mientras el segundo admite terminos compuestos que serán analizados posteriormente. Como de momento solo nos interesa la creación de figuras solo crearemos los comandos de estas:\par

{\codesize
\begin{verbatim}
fmod GEO-SIGNATURE is
  inc FULL-MAUDE-SIGN .

  sorts @GeoCommand@ .
  subsort @GeoCommand@ < @Input@ .

  op point_as_ : @Bubble@ @Token@ -> @GeoCommand@ [ctor] .
  op circumference_in_with`radius_ : @Token@ @Token@ @Bubble@ 
                                     -> @GeoCommand@ [ctor] .
  op line_from_to_ : @Token@ @Token@ @Token@ -> @GeoCommand@ [ctor] .

endfm
\end{verbatim}
}

Como se puede ver la definición es análoga a la de cualquier otra en Maude, con la salvedad de que los tipos suelen darse entre \verb"@" por convención. \par 

Tal y como están definidos los comandos veamos que los puntos se escribirían \verb"point p(0.0 0.0) as p1", las circunferencias \texttt{circumference c1 in p1 with radius 1.0}, y finalmente las rectas como \verb"line r1 from p1 to p2". Como se puede ver todas las figuras menos los puntos dependen de tener otras ya creadas. Esto es así para representar la necesidad de conocer otras figuras para construir unas nuevas.\par

Una vez concluido este módulo nos encargaremos de hacer que Maude entienda lo que queremos hacer, para ello crearemos un módulo de sistema llamado \verb"GEO-DATABASE-HANDLING" en el que ir recogiendo las distintas reglas que se encargarán de interpretar la información. Estas reglas serán analogas a la siguiente:\par

{\codesize
\begin{verbatim}
 crl [point] :
     < O : MUDC | input : ('point_as_[T1 , T2]), dic : D,
                  output : nil, AtS >
  => < O : MUDC | input : nilTermList, dic : (D [Q / F]),
                  output : ('Punto 'almacenado names) , AtS >
  if Q -> F := processPoint(T1 , T2) /\
     names := mod-elements(D [Q / F], Q -> F) .
\end{verbatim}
}
Empezaremos explicando el significado de lo que se acaba de mostrar, la regla nos da una transición entre dos estados del sistema, de los cuales los elementos que nos interesan son los tres centrales, véase, el \texttt{input}, el \texttt{dic}, y el \texttt{output}. El \texttt{input} se corresponde con los comandos puestos arriba, donde \texttt{T1} y \texttt{T2} son el punto y su nombre respectivamente. El \texttt{dic} es lógicamente el diccionario que llevamos en todo momento como estructura de datos donde almacenaremos todas las figuras y sería un \verb"DicFigures". Finalmente el \texttt{output} será la información mostrada por pantalla, en nuestro caso el nombre del punto almacenado. \par

El funcionamiento de la regla comieza calculando \texttt{processPoint(T1, T2)} que nos devolverá una entrada de la forma \texttt{Q -> F}. Una vez hecho esto el diccionario se actualizará mediante la función correspondiente (\verb"dic : (D [Q / F])") con la nueva información. Finalmente indica mediante un output que el punto ha sido almacenado, indicando el nombre. Para esto último se realizará una llamada a la función \texttt{mod-elements} que nos dará los nombres de las figuras que han sido incluidas en el diccionario, pero en el caso de ya pertenecer a él, nos dará el nombre original de estas, véase, con el que se incluyeron la primera vez. Esto es así para evitar dar información incorrecta por pantalla. La función \texttt{mod-elements}, que no se mostrará aquí, se puede encontrar en el módulo auxiliar del que hablaremos a continuación.\par

Como se ha podido ver en la regla anterior, se hace una llamada a la función  \verb"processPoint", esta se encuentra en un módulo funcional auxiliar llamado \verb"GEO-COMMAND-PROCESSING" que se encargará de estas funciones y otras auxiliares como la mencionada antes. Como hemos dado de ejemplo la regla para los puntos veremos a continuación la función \verb"processPoint", que se encargará de convertir los datos de entrada a los tipos que necesitamos mediante otras dos:

{\codesize
\begin{verbatim}
 op processPoint : Term Term -> Entry .
 eq processPoint('bubble[T], 'token[NP]) = downQid(NP) -> processPointAux(T) .

 op processPointAux : Term -> Point .
 ceq processPointAux('__[T, T']) = qid2point(Q, Q')
  if Q := downQid(T) /\
     Q' := downQid(T') .

 op qid2point : Qid Qid -> Point .
 ceq qid2point(Q, Q') = p(F F')
  if S := string(Q) /\
     S' := string(Q') /\
     F := float(S) /\
     F' := float(S') .
\end{verbatim}
}

Veamos lo que hacen las funciones anteriores. \texttt{processPoint} se encarga de extraer el nombre de uno de los términos, en este caso de \verb"'token[NP]", y dar la entrada al diccionario utilizando la función \texttt{processPointAux} que dado el término \verb"p(_ _)" lo descompone, y a través de \texttt{qid2point} lo convierte en un punto como los que definimos atrás. Nos quedaría por tanto explicar las reglas para las rectas y las circunferencias y sus \verb"process", pero se omitirán al ser análogas a la que acabamos de dar.\par

Dadas las construcciones para las tres figuras procederemos a calcular los puntos de corte, para lo cual extenderemos el módulo \verb"GEO-SIGNATURE" con un nuevo comando:
{\codesize
\begin{verbatim}
 op cut`point_and_of_and_ : @Token@ @Token@ @Token@ @Token@ -> @GeoCommand@ [ctor] .
\end{verbatim}
}

Como se puede ver recibe cuatro elementos: los dos primeros corresponden con los nombres de los posibles puntos de corte, recordemos que dos circunferencias se pueden cortar en dos puntos, mientras que los dos últimos son los nombres de las figuras cuyos puntos de corte estamos calculando. La regla correspondiente sería la siguiente: \par
 
{\codesize
\begin{verbatim}
 crl [cut1] :
     < O : MUDC | input : ('cut`point_and_of_and_[T1, T2, 'token[T3],
                  'token[T4]]), dic : D, output : nil, AtS >
  => < O : MUDC | input : nilTermList, dic : (D U [D' , mtD , mtD]),
                  output : ('Puntos 'almacenados names) , AtS >
  if D' := processCutPoint(T1 , T2 , D[downQid(T3)] , D[downQid(T4)]) /\
     names := mod-elements(D U [D' , mtD , mtD], D').
\end{verbatim}
}

Aunque la regla es similar a las ya dadas debe llamarnos la atención \verb"D U [D', mtD, mtD]" dado que no tenemos ninguna función de los diccionarios que sea así. Esta es simplemente una unión de estos, y se puede ver en el módulo correspondiente. Lo verdaderamente importante es el motivo de que esté ahí. En todas las reglas anteriores sabíamos que solo se añadía una figura al diccionario, y por tanto podíamos hacerlo manualmente; el problema es que ahora no sabemos cuántas serán, ya que las figuras pueden no cortarse, ser dos rectas, o dos circunferencias así que se procede a simplemente unir los diccionarios para evitar problemas o la creación de más casos.

Como se puede ver en \verb"processCutPoint(T1, T2, D[downQid(T3)], D[downQid(T4)])" la función \texttt{processCutPoint} no será demasiado complicada, pues recibe directamente figuras sobre las que hacer los cálculos: \par

{\codesize
\begin{verbatim}
 op processCutPoint : Term Term Figure Figure -> Dic .
 ceq processCutPoint('token[T] , 'token[T'] , f1 , f2) =
     processCutPointAux(p1, p2, f1, f2, 1entrada,2entrada)
  if p1 := first-element(cut-point(f1 , f2))
  /\ p2 := first-element(delete-first-element(cut-point(f1 , f2)))
  /\ 1entrada := downQid(T) -> p1
  /\ 2entrada := downQid(T') -> p2 .
\end{verbatim}
}

\texttt{p1} y \texttt{p2} se corresponderán entonces con los dos puntos puntos de corte, y \texttt{1entrada} y \texttt{2entrada} con los las entradas para el diccionario. La función \texttt{processCutPointAux} se encarga de comprobar que no devuelven la misma figura, evitando así que almacenemos el mismo punto dos veces y mostremos por pantalla el nombre de dos figuras iguales, o de una que no existe, pues \texttt{p2} puede ser la lista vacía de puntos. \par

Dados los tres módulos que acabamos de crear tenemos prácticamente todo lo necesario para la entrada y salida de datos. Solo nos quedaría crear un módulo para incluir los comandos que hemos creado en la gramática de Full Maude, y un segundo que se encargará de darnos un entorno. Para comenzar se encargará de dar unos valores iniciales sobre los que trabajar, el diccionario de figuras vacío, \verb"dic : [mtD, mtD, mtD]", así como inicializar el bucle dentro del que se realizan las acciones, en nuestro caso \verb"init-geo", y finalmente controlar que la entrada y la salida funcionan tal y como queremos con distintos casos de errores. Estos dos módulos son \verb"fmod META-GEO-SIGN" y \verb"mod IOGEO2" respectivamente.\par

Veamos ahora cómo funciona la entrada y la salida de \texttt{geoIO.maude} con casos sencillos:\par 

{\codesize
\begin{verbatim}
Maude> load geoIO.maude

	    Full Maude 2.7 March 10th 2015

	GEO: GEO2D a unit testing framework for Maude.
		(March 7th, 2017)

Maude> (point 0.0 0.0 as p1)
Punto almacenado p1
Maude> (point 0.0 1.0 as p2)
Punto almacenado p2
Maude> (line r from p1 to p2)
Recta almacenada r
Maude> (circumference c in p1 with radius 1.0)
Circunferencia almacenada c
\end{verbatim}
}

Parece que todo va bien, pero solo con esta información no es seguro, así que utilizaremos \verb"cont" para comprobar la información almacenada en el diccionario.\par

{\codesize
\begin{verbatim}
Maude> cont .
rewrites: 0 in 0ms cpu (0ms real) (~ rewrites/second)
result System: [(nil).TypeList,< o : GEODatabase | db : 
db(...),input : nilTermList,output : nil,default : 'CONVERSION,dic : [('p1 ->
    p(0.0 0.0)) . 'p2 -> p(0.0 1.0),'r -> r(p(0.0 0.0) p(0.0 1.0)),'c -> c(p(
    0.0 0.0), 1.0)] >,'Circunferencia 'almacenada 'c]
\end{verbatim}
}

Como se puede ver nos da bastante información, pero lo que nos interesa es el diccionario \verb"dic : [('p1 -> p(0.0 0.0)) . 'p2 -> p(1.0 1.0),'r -> r(p(0.0 0.0) p(1.0 1.0))"\\ \verb",'c -> c(p(0.0 0.0), 1.0)]" en el que podemos ver que toda la información se ha almacenado correctamente tal y como queríamos. Ahora solo nos quedaría ver los puntos de corte y habríamos terminado:\par

{\codesize
\begin{verbatim}
Maude> (cut point p3 and p4 of c and r)
Puntos almacenados p2 p3
Maude> cont .
rewrites: 0 in 0ms cpu (0ms real) (~ rewrites/second)
result System: [(nil).TypeList,< o : GEODatabase | db : 
db(...),input : nilTermList,output : nil,default : 'CONVERSION,dic : [('p1 ->
    p(0.0 0.0)) . ('p2 -> p(0.0 1.0)) . 'p3 -> p(0.0 -1.0),'r -> r(p(0.0 0.0)
    p(0.0 1.0)),'c -> c(p(0.0 0.0), 1.0)] >,'Puntos 'almacenados 'p2 'p3]
\end{verbatim}
}

De nuevo se puede ver que funciona perfectamente, ya que los puntos de corte de las dos figuras son \verb"p(0.0 1.0)" y \verb"p(0.0 -1.0)", pero como uno ya estaba en el diccionario no se hace nada, aunque se indica que ha sido añadido para dejar claro que había dos puntos de corte.\par

A continuación veremos algunos casos del test de unidad para las funciones de estos módulos. Al estar trabajando en Full Maude, utilizaremos dos nuevos comandos, además de todos los que vimos para Core Maude, que explicaremos a continuación:
{\codesize
\begin{verbatim}
(munit IOGEO2D is
  loop(init-geo)
\end{verbatim}
}
El primero de ellos es \verb"loop(_)" que se encarga de reiniciar el bucle donde se ejecuta la entrada y salida de datos.\par
{\codesize
\begin{verbatim}
  command(point 0.0 0.0 as p1)
  assertTrue(contains?(@dic, 'p1))
\end{verbatim}
}
El segundo de ellos es \verb"command(_)", que nos permite usar los comandos ya creados en el bucle que acabamos de reiniciar. Además, se puede ver dentro del \verb"assertTrue" que para llamar al diccionario necesitamos escribir \verb"@dic" para indicar que nos referimos al creado en \verb"init-geo". Finalmente el test se cierra con \verb"endu":\par
{\codesize
\begin{verbatim}
endu)
\end{verbatim}
}

Simplemente con estos comandos podríamos dar por terminada esta parte y con ella el proyecto. Sin embargo, para dar unas construcciones de circunferencias más realistas daremos un nuevo comando que en vez de recibir un número real tome como radio  dos puntos:

{\codesize
\begin{verbatim}
   op circumference_in_with`radius`distance`from_to_ : 
      @Token@ @Token@ @Token@ @Token@ -> @GeoCommand@ [ctor] .
\end{verbatim}
}

Las funciones asociadas a este comando, así como la regla correspondiente, son análogas a las vistas para los anteriores, así que no la mostraremos aquí. \par

De forma paralela a la creación de este comando se crea una nueva función que se encargará de dar nombre automáticamente a las puntos de corte calculados, y así simplificar los comandos. Esa función es la siguiente:

{\codesize
\begin{verbatim}
 op new-name : Qid Qid Qid DicFigures -> Qid .
 ceq new-name(name1, name2, Q, DF) = new-name(Q', qid(string(1.0)), Q, DF)
   if Q' := qid(string(name1) + string(name2)) /\
      Q' == Q  . 
 ceq new-name(name1, name2, Q, DF) = new-name(Q', qid(string(1.0)), Q, DF)
   if Q' := qid(string(name1) + string(name2)) /\
      contains?(DF, Q') .
 ceq new-name(name1, name2, Q, DF) = Q'
   if Q' := qid(string(name1) + string(name2)) .
\end{verbatim}
}

La función recibe los nombres de los dos elementos que la forman, como por ejemplo dos rectas, un tercer nombre que servirá, en el caso de tener dos puntos de corte, para evitar que los dos se llamen igual, y finalmente el diccionario total con todas las figuras. La función se encargaría entonces de sumar los nombres de los dos elementos que le forman teniendo cuidado de que no exista ninguna figura anterior con el mismo nombre. El caso de los puntos de corte es distinto ya que los puntos se nombran antes de añadirlos, o actualizarlos, al diccionario.\par

Como se puede ver esta función resulta entonces muy útil saliéndose de su propósito base, y es que dando como tercer \verb"Qid" \verb"'none" podemos conseguir que ninguno de los comandos a excepción de los números necesiten nombrarse. Estos últimos comandos son:

{\codesize
\begin{verbatim}
  op circumference`in_with`radius`distance`from_to_ : @Token@ @Token@ @Token@ ->  
     @GeoCommand@ [ctor] .
  op line`from_to_ : @Token@ @Token@ -> @GeoCommand@ [ctor] .
  op cut`point`of_and_ : @Token@ @Token@ -> @GeoCommand@ [ctor] .
\end{verbatim}
}

Y la regla de los puntos de corte queda como sigue:

{\codesize
\begin{verbatim}
 crl [cut2] :
     < O : MUDC | input : ('cut`point`of_and_['token[T1], 'token[T2]]), dic : D,
                  output : nil, AtS >
  => < O : MUDC | input : nilTermList, dic : (D U [D' , mtD , mtD]),
                  output : ('Puntos 'almacenados names) , AtS >
  if Q := downQid(T1) /\
    Q' := downQid(T2) /\
    name1 := new-name(Q, Q', 'none, D) /\
    name2 := new-name(Q', Q, name1, D) /\
    D' := processCutPoint('token[upTerm(name1)], 'token[upTerm(name2)], D[Q], D[Q']) 
    /\ names := mod-elements(D U [D' , mtD , mtD], D') .
\end{verbatim}
}

El resto de reglas son análogas y se pueden encontrar en el módulo completo disponible en el repositorio, junto con los respectivos casos de test. \par

Con estas últimas reglas damos entonces por terminado este capítulo y el proyecto, con el que hemos conseguido emular perfectamente en Maude la geometría euclídea con regla y compás mientras desarrollábamos una buena cantidad de test de unidad y los módulos de aprendizaje. \par

