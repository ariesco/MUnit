%!TEX root = tfg_fiesta.tex

A lo largo de este capítulo crearemos el \texttt{fmod CIRCUMFERENCE} que nos proporcionará la última de las figuras geométricas que necesitamos. Posteriormente crearemos otro módulo que se encargará de cargar los tres anteriores y aunar en una única función aquellas que nos proporcionan puntos de corte, facilitándonos así el proceso posteriormente en el módulo referido a la entrada y la salida de datos.\par

\section{CIRCUMFERENCE}

Llegamos ya al último módulo funcional para geometría en dos dimensiones, el \texttt{fmod} \\ \texttt{CIRCUNFERENCE}, y es que una vez hayamos conseguido las funciones que nos den los puntos de corte entre dos circunferencias, o entre una de estas y una recta, tendremos todos las funciones necesarias para implementar las construcciones con regla y compás. \par

Comenzaremos con la construcción de las circunferencias, consistiendo estas en un par formado por su centro y su radio: \par

{\codesize
\begin{verbatim}

fmod CIRCUMFERENCE is

   pr LINE .
   sorts Circumference .

   op c : Point Float -> Circumference [ctor] .
   *** Define una circunferencia como un par (punto, real), correspondiendo 
   *** estos con el centro y el radio.
	
   vars p1 p2 p3 p4 : Point .
   vars z11 z12 z21 z22 z31 z32 z41 z42 m n x y 
        Aux-A Aux-B Aux-C Aux-D Aux-E Aux-F Aux-G : Float .
   vars r1 r2 : Line .
   vars v1 v2 : Vector .
   vars c1 c2 c1' c2' : Circumference .
   vars t : Bool .
   vars l l1 : List .
\end{verbatim}
}

De forma paralela crearemos también tres funciones muy sencillas: \texttt{circumference-center}, \texttt{circumference-radius} y \texttt{are-in-circumference}. Las dos primeras simplemente nos devuelven el centro y el radio de la circunferencia, mientras que la tercera, un poco más interesante, nos indica si un punto esta en la circunferencia. Para esto simplemente calcula la distancia del punto al centro y la compara con el radio, si son iguales pertenecerá a esta y nos devolverá \texttt{true}, en caso contrario \texttt{false}: \par
{\codesize
\begin{verbatim}		
   op circumference-center : Circumference -> Point .
   *** Devuelve el centro de la circunferencia
   eq circumference-center(c(p1 , z11)) = p1 .

   op circumference-radius : Circumference -> Float .
   *** Devuelve el radio de la circunferencia
   eq circumference-radius(c(p1 , z11)) = z11 .

   op are-in-circumference : Point Circumference -> Bool .
   *** Comprueba si un punto esta en una circunferencia, 
   ***  comprobando su distanciaal centro con el radio.
   eq are-in-circumference(p1 ,c(p2 , z11)) = distance(p1 , p2) == z11 .
\end{verbatim}  
}

Mientras que con las rectas hubo una gran variedad de funciones, en el caso de las circunferencias nos centraremos en simplemente hallar los puntos de corte comenzando por el caso de recta y circunferencia. Como ya hemos hecho otras veces para empezar crearemos una función que nos dirá si existe el punto de corte, siguiendo los siguientes pasos: trazamos una recta perpendicular a la dada que pase por el centro de la circunferencia y hallamos el punto de corte. A continuación calculamos la distancia de ese punto al centro, si es menor o igual que el radio significara que la recta y la circunferencia se cortan, no habrá corte en caso contrario.\par

{\codesize
\begin{verbatim}
op cut?-r-c : Line Circumference -> Bool .
*** Comprueba si una recta y una circunferencia se cortan mediante la comparacion 
*** de la distancia de la recta al centro de la circunferencia.
eq cut?-r-c(r1 , c(p1 , z11)) = distance(cut-point-r-r(r1 , 
   perpendicular-line(r1 , p1)) , p1) <= z11 .
\end{verbatim}
}

Una vez terminada la función tenemos que ver como conseguir los puntos de corte. El primer problema que nos encontramos es el número, ya que pueden cortarse en uno o dos puntos. Para ello implementaremos en el \texttt{fmod POINT} el tipo \texttt{List} :

{\codesize
\begin{verbatim}
   sorts Point List .
   subsort Point < List .

   op p(__) : Float Float -> Point [ctor] .
   *** Define un punto como un par de numeros reales

   op mt : -> List [ctor] .
   op __ : List List -> List [ctor assoc id: mt] . 
\end{verbatim}
}

La definición que hacemos aquí de las listas es idéntica la que ya hicimos en el primer capítulo, con la salvedad de que ahora son de puntos. Las funciones definidas son entonces la mismas que la otra vez, con solamente dos nuevas, \texttt{first-element} y \texttt{delete-first-element} que respectivamente nos dan el primer elemento de una lista, o nos dan esta sin el primer elemento.\par

{\codesize
\begin{verbatim}
   vars p1 : Point .
   var t : Bool .
   vars l l1 : List .

   op delete-first-element : List -> List .
   *** Borra el primer elemento de una lista.
   ceq delete-first-element(l1 p1) = l1
   if not empty-list(mt l1 p1) .
   ceq delete-first-element(mt) = mt
   if empty-list(mt) .

   op first-element : List -> Point .
   *** Da el primer elemento de una lista.
   eq first-element(mt l1 p1) = p1 .
\end{verbatim}

Sin embargo este no es el único cambio que deberemos realizar en este módulo, dada la forma de las ecuaciones tanto de la recta como de la circunferencia necesitaremos resolver llegado el momento ecuaciones de segundo grado. Así pués implementamos las funciones correspondientes en \texttt{fmod POINT}. \par

{\codesize
\begin{verbatim}
   vars a b c : Float .

   op first-degree-equation : Float Float -> Float .
   *** Devuelve la solucion de una ecuacion de primer grado.
   *** Recibe los valores correspondientes a ax + b.
   eq first-degree-equation(a, b) = - b / a .

   op second-degree-equation-1 : Float Float Float -> Float .
   *** Devuelve el valor de una ecuacion de segundo grado en el caso +
   *** Recibe los valores correspondientes a ax^2 + bx + c.
   eq second-degree-equation-1(0.0 , b , c) = first-degree-equation(b, c) .
   eq second-degree-equation-1(a , b , c) = 
      (- b + sqrt(b ^ 2.0 - (4.0 * a * c))) / (2.0 * a) . 

   op second-degree-equation-2 : Float Float Float -> Float .
   *** Devuelve el valor de una ecuacion de segundo grado en el caso -
   *** Recibe los valores correspondientes a ax^2 + bx + c.
   eq second-degree-equation-2(0.0 , b , c) = first-degree-equation(b, c) .
   eq second-degree-equation-2(a , b , c) = 
      (- b - sqrt(b ^ 2.0 - (4.0 * a * c))) / (2.0 * a) .
\end{verbatim}
}

Terminadas ya todas las funciones auxiliares que necesitaremos solo queda implementar la función de corte. Para ello volveremos a hacer una distinción de casos, siendo esta vez tres. Los dos primeros son aquellos en los que las rectas son verticales u horizontales, ya que eso nos da automaticamente uno de los valores de los puntos. Sin embargo, como puede verse en el programa, no he hecho uso de las funciones definidas en el fmod LINE, sino que se ven si son horizontales o verticales directamente por los puntos que las forman. Por otro lado tenemos el caso general, que calcula por separado los dos puntos de corte y los devuelve en forma de lista. Podemos encontrar los cálculos referentes a estas funciones aquí(incluir referencia). Veamos las funciones:

{\codesize
\begin{verbatim}
   op cut-point-r-c : Line Circumference -> List .
   *** Dados una circunferencia y un punto devuelve una lista con los dos puntos de corte
   *** pudiendo ser estos iguales.
   ceq cut-point-r-c(r(p(z11 z12) p(z11 z22)), c(p(z31 z32), z41)) = p(z11 Aux-A) p(z11 Aux-B)
   *** caso vertical 
   if Aux-A := second-degree-equation-1(1.0, (-2.0 * z32), (z32 ^ 2.0) + ((z11 - z31) ^ 2.0) - (z41 ^ 2.0)) /\
      Aux-B := second-degree-equation-2(1.0, (-2.0 * z32), (z32 ^ 2.0) + ((z11 - z31) ^ 2.0) - (z41 ^ 2.0)) .
   ceq cut-point-r-c(r(p(z11 z12) p(z21 z12)), c(p(z31 z32), z41)) = p(Aux-A z12) p(Aux-B z12)
   *** caso horizontal
   if Aux-A := second-degree-equation-1(1.0, (-2.0 * z31), (z31 ^ 2.0) + ((z21 - z32) ^ 2.0) - (z41 ^ 2.0)) /\
      Aux-B := second-degree-equation-2(1.0, (-2.0 * z31), (z31 ^ 2.0) + ((z21 - z32) ^ 2.0) - (z41 ^ 2.0)) .
   eq cut-point-r-c(r1, c1) = cut-point-r-c-1(r1, c1) cut-point-r-c-2(r1, c1) .

   op cut-point-r-c-1 : Line Circumference -> Point .
   *** Halla el primer punto de corte de la recta y la circunferencia
   ceq cut-point-r-c-1(r1, c(p(z31 z32), z41)) = p(x y)
   if m := equ-line-m(r1) /\
      n := equ-line-n(r1) /\
      Aux-A := 1.0 + (m ^ 2.0) /\
      Aux-B := (2.0 * m * n) - (2.0 * z32 * m) - (2.0 * z31)  /\
      Aux-C := (z31 ^ 2.0) + (z32 ^ 2.0) - (z41 ^ 2.0) - (2.0 * n * z32) + (n ^ 2.0)  /\
      x := second-degree-equation-1(Aux-A, Aux-B, Aux-C) /\
      y := m * x + n .

   op cut-point-r-c-2 : Line Circumference -> Point .
   *** Halla el segundo punto de corte de la recta y la circunferencia
   ceq cut-point-r-c-2(r1, c(p(z31 z32), z41)) = p(x y)
   if m := equ-line-m(r1) /\
      n := equ-line-n(r1) /\
      Aux-A := 1.0 + (m ^ 2.0) /\
      Aux-B := (2.0 * m * n) - (2.0 * z32 * m) - (2.0 * z31)  /\
      Aux-C := (z31 ^ 2.0) + (z32 ^ 2.0) - (z41 ^ 2.0) - (2.0 * n * z32) + (n ^ 2.0) /\
      x := second-degree-equation-2(Aux-A, Aux-B, Aux-C) /\
      y := m * x + n .

\end{verbatim}
}

Concluida ya esta función nos ponemos con la última operación que necesitaremos, calcular los puntos de corte de dos circunferencias. Lo primero que haremos será construir la función que nos indique si las dos figuras se cortan, como ya hicimos en los otros casos. Esta vez la idea de la función resulta mucho más sencilla que antes, simplemente comprobamos la distancia entre los centros y vemos si es menor o igual que la suma de los radios: \par
{\codesize
\begin{verbatim}
   op cut?-c-c : Circumference Circumference -> Bool .
   *** Comprueba si dos circunferencias se cortan en base a la distancia de los centros.
   ceq cut?-c-c(c1, c2) = distance(circumference-center(c1) , circumference-center(c2)) <= (circumference-radius(c1) + circumference-radius(c2))
   if not (circumference-center(c1) == circumference-center(c2)) . 
   ceq cut?-c-c(c1, c2) = circumference-radius(c1) == circumference-radius(c2)
   if (circumference-center(c1) == circumference-center(c2)) .
\end{verbatim}
}

Sin embargo hallar los puntos de corte sí se complica, y es que, si las dos circunferencias poseen centro con la coordenada x igual, nos surgirá más adelante una división entre cero, así que en este caso iremos por otro camino, que dependerá de que sean distintas las coordenadas y, esto se debe a que haremos una simetría respecto de la recta y = x para hallar los puntos de corte, y luego los traspondremos, lo cual también corresponde con hacer una simetría. Este proceso simplemente se hace para evitar tener que hacer una nueva distinción de casos, pues los cálculos serían totalmente análogos. \par
{\codesize
\begin{verbatim}
   op cut-point-c-c : Circumference Circumference -> List .
   ceq cut-point-c-c(c(p(z11 z12) , z31) , c(p(z21 z22) , z32)) = cut-point-circumferences-x-dis(c(p(z11 z12) , z31) , c(p(z21 z22) , z32)) 
   if not (z11 == z21) .
   ceq cut-point-c-c(c(p(z11 z12) , z31) , c(p(z21 z22) , z32)) = cut-point-circumferences-y-dis(c(p(z11 z12) , z31) , c(p(z21 z22) , z32))
   if not (z12 == z22) .
   ceq cut-point-c-c(c(p(z11 z12) , z31) , c(p(z21 z22) , z32)) = mt 
   if p(z11 z12) = p(z21 z22) /\ not(z31 == z32) .

   op cut-point-circumferences-x-dis : Circumference Circumference -> List .
   *** da una lista con dos puntos, que serian los puntos de corte de las dos circunferencias.
   *** solo funciona si los centros no tienen la misma X.
   ceq cut-point-circumferences-x-dis(c1 , c2) = p1 p2
   if p1 := cut-point-circumferences-1(c1 , c2) 
   /\ p2 := cut-point-circumferences-2(c1 , c2) .

   op cut-point-circumferences-y-dis : Circumference Circumference -> List .
   *** da una lista con dos puntos, que serian los puntos de corte de las dos circunferencias.
   *** solo funciona si los centros no tienen la misma X.
   ceq cut-point-circumferences-y-dis(c(p(z11 z12) , z31) , c(p(z21 z22) , z32)) = p1 p2
   if p1 := trans-point(cut-point-circumferences-1(c(p(z12 z11) , z31) , c(p(z22 z21) , z32))) 
   /\ p2 := trans-point(cut-point-circumferences-2(c(p(z12 z11) , z31) , c(p(z22 z21) , z32))) .

\end{verbatim}
}

Ahora simplemente damos las funciones que nos devuelven los puntos propiamente dichos y listo. Como ocurría con los demás puntos de corte estos cálculos no son triviales, y pueden verse aquí(incluir referencia): 
{\codesize
\begin{verbatim}
   op cut-point-circumferences-1 : Circumference Circumference -> Point .
   *** halla el primer punto de corte de las dos circunferencias.
   ceq cut-point-circumferences-1(c(p(z11 z12) , z31) , c(p(z21 z22) , z32)) = p((Aux-A - (second-degree-equation-1(Aux-C , Aux-D , Aux-E) * Aux-B))  second-degree-equation-1(Aux-C , Aux-D , Aux-E))
   if Aux-A := (z31 ^ 2.0 - z32 ^ 2.0 + (z21 ^ 2.0 + z22 ^ 2.0) - (z11 ^ 2.0 + z12 ^ 2.0)) / (2.0 * z21 - 2.0 * z11) 
   /\ Aux-B := (z22 - z12) / (z21 - z11) 
   /\ Aux-C := (Aux-B) ^ 2.0 + 1.0
   /\ Aux-D := 2.0 * Aux-B * (z11 - Aux-A) - 2.0 * z12
   /\ Aux-E := Aux-A * (Aux-A - 2.0 * z11) - z11 ^ 2.0 + z12 ^ 2.0 - z31 ^ 2.0 .

   op cut-point-circumferences-2 : Circumference Circumference -> Point .
   *** halla el segundo punto de corte de las dos circumferences.
   ceq cut-point-circumferences-2(c(p(z11 z12) , z31) , c(p(z21 z22) , z32)) = p((Aux-A - (second-degree-equation-2(Aux-C , Aux-D , Aux-E) * Aux-B))  second-degree-equation-2(Aux-C , Aux-D , Aux-E))
   if Aux-A := (z31 ^ 2.0 - z32 ^ 2.0 + (z21 ^ 2.0 + z22 ^ 2.0) - (z11 ^ 2.0 + z12 ^ 2.0)) / (2.0 * z21 - 2.0 * z11) 
   /\ Aux-B := (z22 - z12) / (z21 - z11) 
   /\ Aux-C := (Aux-B) ^ 2.0 + 1.0
   /\ Aux-D := 2.0 * Aux-B * (z11 - Aux-A) - 2.0 * z12
   /\ Aux-E := Aux-A * (Aux-A - 2.0 * z11) - z11 ^ 2.0 + z12 ^ 2.0 - z31 ^ 2.0 .
\end{verbatim}
}

A continuación se muestran algunos casos del test de unidad correspondiente a las funciones para circunferencias. \par
{\codesize
\begin{verbatim}
(munit CIRCUMFERENCE is 
	assertEqual(circumference-center(c(p(1.0 2.0), 3.0)), p(1.0 2.0))
	assertEqual(circumference-radius(c(p(0.0 0.0), 3.4)), 3.4)
	assertTrue(are-in-circumference(p(1.0 0.0), c(p(0.0 0.0), 1.0)))
	assertTrue(cut?-r-c(r(p(-1.0 1.0) p(1.0 1.0)), c(p(0.0 0.0), 2.0)))
	assertEqual(cut-point-r-c-1(r(p(0.0 0.0) p(1.0 1.0)), c(p(1.0 0.0), 1.0)), p(1.0 1.0))
	assertEqual(cut-point-r-c-2(r(p(0.0 -2.0) p(1.0 0.0)), c(p(3.0 2.0), 1.0)), p(2.0 2.0))
	assertEqual(cut-point-r-c(r(p(0.0 2.0) p(2.0 2.0)), c(p(0.0 0.0), 2.0)), p(0.0 2.0) p(0.0 2.0))
	assertTrue(cut?-c-c(c(p(0.0 0.0), 1.0), c(p(2.0 0.0), 1.0)))
 	assertEqual(cut-point-circumferences-1(c(p(0.0 0.0), 2.0), c(p(2.0 2.0), 2.0)), p(0.0 2.0))
 	assertEqual(cut-point-circumferences-2(c(p(0.0 0.0), 2.0), c(p(2.0 2.0), 2.0)), p(2.0 0.0))
	assertEqual(cut-point-circumferences-y-dis(c(p(2.0 0.0), 1.0), c(p(2.0 2.0), 1.0)), p(2.0 1.0) p(2.0 1.0))
	assertEqual(cut-point-c-c(c(p(0.0 0.0), 2.0), c(p(2.0 2.0), 2.0)), p(0.0 2.0) p(2.0 0.0))
endu)
\end{verbatim}
}

Con esto damos fín al módulo de circunferencias. \par

\section{GEO2D}
Como se puede ver, hay puntos de corte entre muchos tipos de figuras, pero según sean estas de un tipo u otro tendremos que usar distintas funciones. Por ello creamos un último módulo llamado GEO2D, que solo incluirá una función llamada \texttt{cut-point}, que dadas dos figuras cuales quiera, devuelve sus puntos de corte. \par

Para ello empezaremos por crear un nuevo tipo, llamado \texttt{Figure}, que simplemente contendrá a los demás tipos, haciendo así que nos sea totalmente indiferente trabajar con una u otra figura llegado el momento:\par
{\codesize
\begin{verbatim}
fmod GEO2D is

   pr CIRCUMFERENCE .

   sort Figure .
   subsort Point < Figure .
   subsort Line < Figure .
   subsort Circumference < Figure .

   vars p1 p2 : Point .
   vars r1 r2 : Line .
   vars c1 c2 : Circumference .

   op cut-point : Figure Figure -> List [comm] .
   *** Comprueba si dos figuras se cortan, y devuelve una lista con los puntos de corte
   ceq cut-point(p1, p2) = p1
   if p1 == p2 .
   ceq cut-point(p1 , r1) = p1
   if are-in-line(p1 , r1) .
   ceq cut-point(r1 , r2) = cut-point-r-r(r1 , r2)
   if cut?-r-r(r1 , r2) .
   ceq cut-point(p1 , c1) = p1
   if are-in-circumference(p1 , c1) .
   ceq cut-point(r1 , c1) = cut-point-r-c(r1 , c1)
   if cut?-r-c(r1 , c1) .
   ceq cut-point(c1 , c2) = cut-point-c-c(c1 , c2)
   if cut?-c-c(c1 , c2) .
   ceq cut-point(p1, p2) = mt
   if not p1 == p2 .	
   ceq cut-point(p1 , r1) = mt
   if not are-in-line(p1 , r1) .
   ceq cut-point(r1 , r2) = mt 
   if not cut?-r-r(r1 , r2) .
   ceq cut-point(p1 , c1) = mt
   if not are-in-circumference(p1 , c1) .
   ceq cut-point(r1 , c1) = mt
   if not cut?-r-c(r1 , c1) .
   ceq cut-point(c1 , c2) = mt
   if not cut?-c-c(c1 , c2) .

endfm	
\end{verbatim}
}
Como se puede ver se han incluido también algunas combinaciones un poco raras, como el punto de corte entre dos puntos, pero esto es solo para evitar que el programa de problemas en el futuro.


