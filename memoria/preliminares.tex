%!TEX root = tfg_fiesta.tex

A lo largo de este capítulo se explicará qué son Maude, los test de unidad y los módulos de aprendizaje, aprovechando lo cual se pasará a dar ejemplos de los primeros módulos con sus casos de test. Asimismo, haremos también mención a las herramientas que utilizaremos a los largo del proyecto.\par

\section{Maude}

Maude~\cite{maudeBook} es un lenguaje declarativo, véase, sus programas se construyen declarando condiciones, ecuaciones o transformaciones que definen el problema y nos dan una solución, la cual no será construida siguiendo un conjunto de pasos sino que será el intérprete el encargado de deducirla en base a las funciones definidas. Maude presenta dos tipos distintos de módulos, que nos permiten modelizar distintos tipos de problemas.\par

El primero de estos es el módulo funcional. En él daremos una serie de tipos, subtipos y relaciones entre ellos, las cuales estarán dadas en forma de ecuaciones que, aunque denotan igualdades, desde el punto de vista del cómputo se ejecutan de izquierda a derecha. Además de esto nos encontraremos tanto en los operadores como en las relaciones o los constructores distintos axiomas, como la conmutatividad o la asociatividad. De esta manera, cuando Maude interpreta estos módulos lo que hace es utilizar las distintas ecuaciones para deducir un resultado encajando los términos módulo el conjunto de axiomas.\par

Por otro lado nos encontramos con los módulos de sistema. Estos funcionan de manera similar a los funcionales con la salvedad de que, además de tener relaciones dadas por ecuaciones, permite definir transiciones. Estas estarán dadas por diferentes reglas que, de manera similiar a las ecuaciones de los módulos funcionales, se ejecutarán de izquierda a derecha, pero en este caso no denotan igualdades sino cambios de estado.\par

Para ilustrar estos conceptos se procederá a la creación de dos ejemplos. \par

\subsection{Ejemplo de módulo funcional}

Como ejemplo de módulo funcional implementaremos los naturales de Peano con diferentes operaciones. En Maude los módulos funcionales se crean siguiendo el siguiente esquema: \texttt{fmod} $\langle$Nombre del Módulo$\rangle$ \texttt{is} $\langle$Definiciones y Declaraciones$\rangle$ \texttt{endfm}. Así, el módulo\\ \texttt{PEANO-NAT-EXTRA} comienza como sigue: \par

{\codesize
\begin{verbatim}
fmod PEANO-NAT-EXTRA is
\end{verbatim}
}

Es posible definir tipos de datos mediante \texttt{sort}, y \texttt{sorts} en el caso en que sean varios. Por ejemplo, en el caso de los naturales (\texttt{Nat}), los naturales distintos de cero (\texttt{NoZeroNat}) y el cero (\texttt{ZeroNat}) se definen como:\par

{\codesize
\begin{verbatim}
   sorts Nat NoZeroNat ZeroNat .
\end{verbatim}
}

Dentro de los tipos se les puede dotar de orden con la palabra reservada \texttt{subsort}, por ejemplo: \par

{\codesize
\begin{verbatim}
   subsort NoZeroNat < Nat .
   subsort ZeroNat < Nat .
\end{verbatim}
}

En este caso solo definiremos los \texttt{ZeroNat} y \texttt{NoZeroNat}, sin embargo en las funciones el dominio serán siempre los \texttt{Nat}. Retomando la definición de estos, antes de utilizarlos suele ser necesario darles un constructor para poder trabajar con ellos. Para ello utilizaremos la palabra reservada \texttt{op}, los tipos de los argumentos y del resultado, y por último indicaremos que es un constructor con el atributo \texttt{ctor}. \par

{\codesize
\begin{verbatim}
   op 0 : -> ZeroNat [ctor] .
   op s : Nat -> NoZeroNat [ctor iter] .
\end{verbatim}
}

Como se puede observar, a los constructores se les puede dotar de axiomas como \texttt{iter}, que le permite acortar los términos construidos con ese operador, por ejemplo, \verb"s(s(0))" sería lo mismo que \verb"s^2(0)", otros axiomas importantes serían \texttt{comm}, \texttt{assoc} que le dotarían de las propiedades de commutatividad y asociatividad respectivamente. Continuando con la creación del módulo, antes de definir ninguna función será necesario hacer una declaración de variables en base a sus tipos mediante el comando \texttt{var}, o \texttt{vars} terminando con el tipo correspondiente.\footnote{También existe la posibilidad de declarar las variables al vuelo en las propias ecuaciones de la función, como se vera más adelante.} \par

{\codesize
\begin{verbatim}
   vars M N R : Nat .
\end{verbatim}
}

La creación de las funciones es igual a la de los constructores, pero en este caso no tendrán el axioma \texttt{ctor} y contarán con ecuaciones. Por otro lado hay que dotar a las funciones de forma, esto se puede hacer simplemente indicando un nombre o mediante una construcción del tipo \verb"_+_" donde \verb"_" indica la posición de los valores de entrada, quedando entonces como sigue: \par

{\codesize
\begin{verbatim}
   op add : Nat Nat -> Nat [comm assoc] .     op _+_ : Nat Nat -> Nat [comm assoc] .
\end{verbatim}
}

Una vez definida la función habrá que dotarla de comportamiento mediante ecuaciones, las cuales como ya se ha comentado antes, se ejecutarán de izquierda a derecha. Estas comenzarán por \texttt{eq}, o \texttt{ceq} para ecuaciones condicionales, e indicarán mediante una igualdad el resultado de la operación. A la hora de dar las ecuaciones dependemos de la definición de la operación, siendo las ecuaciones del primer caso para \verb"add" izquierda, y las del segundo para \verb"_+_": \par
{\codesize
\begin{verbatim}
   eq add(0 , N) = N .                        eq 0 + N = N .
   eq add(s(M) , N) = s(add(M , N)) .         eq s(M) + N = s(M + N) .
\end{verbatim}
}

Como se indicó anteriormente, es posible declarar variables al vuelo como sigue: \par
{\codesize
\begin{verbatim}
   eq 0 + N:Nat = N:Nat .
   eq s(M:Nat) + s(N:Nat) = s(M:Nat + N:Nat) 
\end{verbatim}
}
 
El método utilizado para dar las variables dependerá de las veces que se vaya a usar esta a lo largo del módulo, pero por comodidad se seguirá utilizando la declaración de variables inicial. Retomando las funciones, esta no es la única forma que tenemos de darlas, sino que en los casos de condicionales tendremos que usar el comando \texttt{ceq} así como dar con \texttt{if} las condiciones necesarias, las cuales se unen mediante el operador \verb"_/\_". Estas pueden ser expresiones Booleanas, comprobaciones de tipo y condiciones de encaje de patrones. A continuación mostraremos condiciones Booleanas, más información está disponible en ~\cite{maudeBook}
{\codesize
\begin{verbatim}
   op _*_ : Nat Nat -> Nat [comm] .
   ceq M * N = 0 
   if M == 0.
   ceq s(M) * N = (M * N) + N
   if not (s(M) == 0) .
\end{verbatim}
}

De la misma manera creamos la igualdad, \verb"_eq_", y la función menor que, \verb"_<_".\par

{\codesize
\begin{verbatim}
   op _eq_ : Nat Nat -> Nat [comm] .          op _<_ : Nat Nat -> Bool .
   eq 0 eq s(M) = false .                     eq 0 < s(M) = true .
   eq N eq N = true .                         eq s(M) < s(N) = M < N .
   eq M eq N = false [owise] .                eq M < N = false [owise] .
\end{verbatim}
}
Se puede ver en ambas funciones la aparición del atributo \verb"[owise]" en una ecuación de cada una. La función de este es indicar que, en caso de no encajar los atributos de entrada con ninguna de las ecuaciones anteriores, se deberá ejecutar esta. \par

Una vez hayamos creado e incluido todas las funciones será necesario indicar la terminación del módulo mediante: \par

{\codesize
\begin{verbatim}
endfm
\end{verbatim}
}

Veamos ahora un ejemplo de ejecución del módulo funcional que acabamos de crear. Para comenzar deberemos cargar el módulo desde la consola de Maude.\par

{\codesize
\begin{verbatim}
Maude> load PEANO-NAT-EXTRA.maude
\end{verbatim}
}

El intérprete de Maude no ejecuta los programas como tal, sino que lee lo que hayamos escrito y lo reduce lo máximo posible utilizando las ecuaciones implementadas. Deberemos escribir \texttt{red} $\langle$Expresión que queremos reducir$\rangle$ \verb" .", teniendo cuidado con el punto. Veamos algunos casos sencillos:
{\codesize
\begin{verbatim}
Maude> red s(0) .
reduce in PEANO-NAT-EXTRA : s(0) .
rewrites: 0 in 0ms cpu (0ms real) (~ rewrites/second)
result NoZeroNat: s(0)
\end{verbatim}
}
\noindent Como se puede observar Maude indica el tipo mínimo del término resultado (\texttt{NoZeroNat}), que en este caso es el mismo que el inicial porque no se le pueden aplicar ecuaciones. Este también nos indica el tiempo de ejecución.

Aunque la función que hemos ejecutado resulta sencilla podemos ver que todo funciona correctamente, veamos otro caso más, este sí con mayor complejidad al tener que aplicar al menos dos ecuaciones:\par

{\codesize
\begin{verbatim}
Maude> red (s(0) + s(s(0))) * s(s(0)) .
reduce in PEANO-NAT-EXTRA : (s(0) + s^2(0)) * s^2(0) .
rewrites: 15 in 0ms cpu (0ms real) (~ rewrites/second)
result NoZeroNat: s^6(0)
\end{verbatim}
}

\noindent En este caso vemos que lo primero que ha hecho Maude ha sido utilizar el axioma \texttt{iter} para reducir las expresiones y después reducir la expresión, lo cual ha realizado con 15 reescrituras.

\subsection{Ejemplo de módulo de sistema}

El módulo que crearemos de ejemplo para los módulos de sistema será una representación del problema de las vasijas~\cite[capítulo 7]{maudeBook}. Este problema consiste en, dadas $n$ vasijas con distintas capacidades, las cuales podermos llenar del todo, vaciar cuando queramos y verter de una a otra hasta vaciar la primera o llenar la segunda, conseguir que en una de ellas nos quede una cantidad de líquido concreta. El caso más común es aquel en que tenemos $3$ recipientes con una capacidad de 3, 5 y 8 litros respectivamente, y buscamos conseguir que en uno de ellos queden 4 litros de agua. \par

En Maude los módulos de sistema se crean siguiendo el siguiente esquema: \texttt{mod} $\langle$Nombre del Módulo$\rangle$ \texttt{is} $\langle$Definiciones, Declaraciones y Reglas$\rangle$ \texttt{endm}. Empezaremos definiendo el módulo \texttt{VASIJAS}: \par

{\codesize
\begin{verbatim}
mod VASIJAS is
\end{verbatim}
}

Los comandos básicos, como importar, dar los tipos, constructores y variables mantienen la misma sintaxis que en los módulos funcionales. Como necesitamos un conjunto númerico para la creación del tipo \texttt{Vasija} comenzaremos importando los números naturales. Para importar un módulo tenemos tres opciones, \texttt{protecting}, \texttt{extending} y \texttt{including}, sin embargo en general utilizaremos \texttt{protecting}, de la forma \texttt{pr}, ya que nos importa los módulos pero no nos permite modificarlos, evitando así confusión y problemas de interacción con otros. Así pues, importamos el módulo predefinido \texttt{NAT}. \par

{\codesize
\begin{verbatim}
   protecting NAT .
   sort Vasija CjVasija .
   subsort Vasija < CjVasija .

   op vasija : Nat Nat -> Vasija [ctor] .
   *** El primer numero se corresponde con la cantidad de liquido que contiene 
       la vasija y el segundo con su capacidad maxima.
   op _,_ : Vasija Vasija -> CjVasija [ctor comm assoc] .

   vars C1 C2 N1 N2 : Nat .
\end{verbatim}
}
Como se puede ver las vasijas están definidas como un par, en el que el primer elemento se corresponde con la cantidad de líquido que contiene y el segundo con la capacidad total de esta. Aparte del tipo \texttt{Vasija} creamos tambien el conjunto de estas, \texttt{CjVasijas}, en el que almacemaremos varias vasijas a la vez.\par 

Una vez completados los constructores y la declaración de variables si se desea, procedemos a construir el módulo en sí. Para ello, aparte de las reglas que veremos más adelante, podemos crear funciones mediante ecuaciones al igual que hacíamos en los módulos funcionales. En nuestro caso la única que definiremos será sobre la que queremos que funcionen las reglas, véase, las tres vasijas antes comentadas vacías.\par

{\codesize
\begin{verbatim}
   op inicial : -> CjVasija .
   *** Da un conjunto inicial de vasijas vacias con distintas capacidades.
   eq inicial = vasija(0, 3) , vasija(0, 5) , vasija(0, 8) .
\end{verbatim}
}

Hecho esto podemos ponernos con las reglas en sí. Estas deben comenzar por \verb"rl", y de manera opcional a continuación se las puede identificar mediante \verb"[_]" donde \verb"_" es el nombre que damos a la regla. Después esta se construye como una transición de izquierda a derecha dada por \verb"=>", por ejemplo la regla \texttt{vacia}, que se encarga de vaciar un vasija se daría como sigue:\par

{\codesize
\begin{verbatim}
   rl[vacia] : vasija(N1, C1) => vasija(0, C1) .
   ***Esta regla nos vacia la vasija.
\end{verbatim}
}
De manera análoga a la anterior creamos la regla \texttt{llena}, que nos llena la vasija hasta su capacidad máxima.\par
{\codesize
\begin{verbatim}
   rl[llena] : vasija(N1, C1) => vasija(C1, C1) .
   *** Esta regla nos llena la vasija al maximo.
\end{verbatim}
}
Por supuesto, igual que ocurría con las ecuaciones, podemos construir reglas condicionales. En este caso comenzarían por \verb"crl" y se construirían igual que las anteriores, indicando eso sí mediante un \texttt{if} la condición que deben cumplir.\par

En nuestro caso crearemos las funciones que nos transfieren el líquido de una vasija a la siguiente.\par 
{\codesize
\begin{verbatim}
   crl[transferir1] : vasija(N1, C1) , vasija(N2, C2) 
                    => vasija(0, C1) , vasija((N1 + N2), C2) 
   if (N1 + N2) <= C2 .
   *** Nos transfiere el contenido de una vasija a otra si la suma de 
       los contenidos no se pasa de la capacidad de la segunda.

   crl[transferir2] : vasija(N1, C1) , vasija(N2, C2) 
                    => vasija(sd((N1 + N2) , C2), C1) , vasija(C2, C2)
   if (N1 + N2) > C2 .
   *** Nos transfiere el contenido de una vasija a otra si la suma de 
       los contenidos se pasa de la capacidad de la segunda.
\end{verbatim}
}
Aunque las dos reglas se refieren a la misma acción cubren casos distintos, siendo la primera aquella en que el contenido de las dos vasijas cabe en la segunda, a la que se quiere transferir, y la segunda aquella en la que no, quedando la primera entonces con el líquido sobrante. El módulo finalmente se cierra con \texttt{endm}.
{\codesize
\begin{verbatim}
endm
\end{verbatim}
}


Veamos ahora un ejemplo de ejecución del módulo de sistema que acabamos de crear. Por supuesto debemos comenzar cargando el módulo VASIJAS :\par

{\codesize
\begin{verbatim}
Maude> load VASIJAS.maude
\end{verbatim}
}

Además del comando \texttt{red}, que ya vimos en los módulos funcionales, los de sistema tienen más opciones disponibles. El comando básico es \texttt{rewrite}, abreviado \texttt{rew}, que aplica las ecuaciones y las reglas del módulo al término dado como argumento. Sin embargo, este ejemplo no termina (es posible vaciar todo el tiempo
una vasija vacía, por ejemplo), así que tendremos que indicar manualmente el número de reglas que queremos utilizar en total, por ejemplo 10:

{\codesize
\begin{verbatim}
Maude> rew [10] inicial .
rewrite [10] in VASIJAS : inicial .
rewrites: 139 in 0ms cpu (5ms real) (~ rewrites/second)
result [CjVasija]: vasija(0, 3),vasija(0, 5),vasija(0, 8)
\end{verbatim}
}

Al parecer no ha ocurrido nada, probablemente porque Maude haya utilizado diez veces la regla que vacía una vasija, así que hay que obligarle a utilizar más reglas. Para ello utilizaremos el commando \texttt{frew}, fair rewrite o reescritura justa.\par

{\codesize
\begin{verbatim}
Maude> frew [10] inicial .
frewrite in VASIJAS : inicial .
rewrites: 22 in 0ms cpu (201ms real) (~ rewrites/second)
result (sort not calculated): vasija(0, 8),vasija(3, 3),vasija(5, 5)
\end{verbatim}
}

Podemos ver que en este caso se han llenado las dos ultimas vasijas, pero esto tampoco nos aporta mucha información. Para obtener información relevante necesitamos buscar una solución concreta y que Maude nos la construya, pudiendo consultar después los pasos que ha dado. Para esto utilizaremos el comando \texttt{search}

{\codesize
\begin{verbatim}
Maude> search [1] inicial =>* vasija(4, N:Nat) , B:CjVasija .
\end{verbatim}
}

Antes de mostrar la ejecución explicaré lo escrito. \verb"[1]" indica que solo buscamos una solución, \verb"inicial =>* " nos indica que utilizamos la función \texttt{inicial} partiendo de ella, y que el estado final lo queremos alcanzar en cualquier número de pasos (las otras opciones son \verb"=>+", que requiere 1 o más pasos, \verb"y =>!", que solo devuelve estados finales, es decir, expresiones
a las que no se les pueden aplicar ecuaciones ni reglas), por último \verb"N:Nat" y \verb"B:CjVasija" son variables que nos indican el tipo de los datos que deben ocupar esa posición, pero que pueden tomar cualquier valor. Veamos ahora la ejecución.\par

{\codesize
\begin{verbatim}
Maude> search [1] inicial =>* vasija(4, N:Nat) , B:CjVasija .
search in VASIJAS : inicial =>* B:CjVasija,vasija(4, N:Nat) .

Solution 1 (state 75)
states: 76  rewrites: 2142 in 20ms cpu (48ms real) (107100 rewrites/second)
B:CjVasija --> vasija(3, 3),vasija(3, 8)
N:Nat --> 5
\end{verbatim}
}

Como se puede ver la solución encontrada es aquella en la que la vasija con cuatro litros es aquella que tenía cinco de capacidad, y las otras dos quedan con tres litros cada una. Esta solución ha sido alcanzada en el estado 75, y se puede consultar con el comando \verb"show path 75", que nos indica cuándo y dónde se ejecutaron qué reglas: \par

{\codesize
\begin{verbatim}
Maude> show path 75 .
state 0, [CjVasija]: vasija(0, 3),vasija(0, 5),vasija(0, 8)
===[ rl vasija(N1, C1) => vasija(C1, C1) [label llena] . ]===>
state 2, [CjVasija]: vasija(0, 3),vasija(0, 8),vasija(5, 5)
===[ crl vasija(N1, C1),vasija(N2, C2) => vasija(C2, C2),vasija(sd(C2, N1 +
    N2), C1) if N1 + N2 > C2 = true [label transferir2] . ]===>
state 9, [CjVasija]: vasija(0, 8),vasija(2, 5),vasija(3, 3)
===[ crl vasija(N1, C1),vasija(N2, C2) => vasija(0, C1),vasija(N1 + N2, C2) if
    N1 + N2 <= C2 = true [label transferir1] . ]===>
state 20, [CjVasija]: vasija(0, 3),vasija(2, 5),vasija(3, 8)
===[ crl vasija(N1, C1),vasija(N2, C2) => vasija(0, C1),vasija(N1 + N2, C2) if
    N1 + N2 <= C2 = true [label transferir1] . ]===>
state 37, [CjVasija]: vasija(0, 5),vasija(2, 3),vasija(3, 8)
===[ rl vasija(N1, C1) => vasija(C1, C1) [label llena] . ]===>
state 55, [CjVasija]: vasija(2, 3),vasija(3, 8),vasija(5, 5)
===[ crl vasija(N1, C1),vasija(N2, C2) => vasija(C2, C2),vasija(sd(C2, N1 +
    N2), C1) if N1 + N2 > C2 = true [label transferir2] . ]===>
state 75, [CjVasija]: vasija(3, 3),vasija(3, 8),vasija(4, 5)
\end{verbatim}
}

\subsection{Loop Mode y Full Maude}

Los módulos interactivos en Maude se implementan usando el módulo
Loop Mode~\cite[capítulo 17]{maudeBook}, un módulo que permite
hacer E/S por pantalla pero que require que el usuario siga los siguientes pasos:
\begin{itemize}
\item
Definir la sintaxis de la aplicación que queremos desarrollar.

\item
Analizar sintácticamente en la gramática (meta-representada) los comandos introducidos
mediante reglas de reescritura.

\item
Definir, también mediante reglas (y posiblemente ecuaciones auxiliares) el comportamiento
de los comandos.
\end{itemize}

Un caso particular de aplicación usando el Loop Mode es Full Maude~\cite[parte 2]{maudeBook}.
Full Maude es una extensión de Maude implementada en el propio Maude\footnote{Cuando estamos
interesados en distinguir el subconjunto de Maude implementado en C++ presentado en las
secciones anteriores nos referiremos a él como Core Maude.} Full Maude cuenta con una base de
datos de módulos explícita, implementa los comandos de Core Maude usando el metanivel de
Maude~\cite[capítulo 14]{maudeBook} y además proporciona extensiones como soporte para
especificaciones orientadas a objetos. Por ello, Full Maude es a menudo usado como base para
otras extensiones del sistema. En el caso que nos ocupa, es interesante apuntar que mUnit
es una extensión de Full Maude y que nuestro módulo interactivo (ver sección~\ref{cap.4})
usa funciones auxiliares desarrolladas para Full Maude para simplificar el análisis sintáctico
de los comandos.

%Core Maude es el intérprete del lenguaje implementado en C++ y que nos proporciona todas las funcionalidades básicas de Maude, todos los módulos desarrollados anteriormente en los ejemplos están realizados para poder ejecutarse en Core Maude directamente. Full Maude por otro lado es una extensión de Maude programada en el propio lenguaje que se encarga de proporcionarnos más herramientas para el desarrollo de módulos y que deberá ser cargada, ya sea en el fichero que se vaya a utilizar o desde la propia consola de Maude como si fuese un módulo más. Full Maude nos proporciona nuevas opciones muy interesantes a la hora de crear nuestros programas dándonos por ejemplo la posibilidad de utilizar la entrada/salida de datos, con la que trabajaremos mucho, o la creación de los módulos orientados a objetos, que no trataremos en este proyecto.\par 


\section{Los test de unidad y mUnit}

Un test de unidad~\cite{unitTests} es un fragmento de código que llama a una función, o unidad de trabajo, y comprueba el resultado final de los programas o funciones que contiene. En caso de que uno de los casos resulte falso diremos que el test ha fallado.\par

Un test de unidad bien diseñado debe cumplir las siguientes condiciones~\cite{unitTests}: \par

\begin{itemize}
\item Debe estar automatizado.
\item Debe ser fácil de implementar.
\item No debe quedar obsoleto.
\item Ejecutarlo debe ser tan sencillo como pulsar un botón.
\item Debe tener una ejecución rápida.
\item Debe ser consistente con sus resultados. 
\item Debe tener control total sobre el módulo donde se ejecuta el test, véase, ejecutar todos los casos posibles en todas las funciones con que se encuentre.
\item Debe ser independiente de la ejecución de otros test.\footnote{Esto no ocurre estrictamente en nuestro caso, porque tenemos E/S y es necesario volver a los valores iniciales después de cada ejecución. Esto se explicará con mayor detalle en el capítulo correspondiente.}
\item En caso de que falle, debe ser sencillo encontrar donde se originó el error.
\end{itemize}.

Para trabajar con los test utilizaremos una herramienta ya creada llamada mUnit (incluir referencia web), que nos proporciona distintas herramientas para verificar el funcionamiento de las funciones. Estas son: \verb"assertEqual", \verb"assertDifferent", \verb"assertTrue", \verb"assertFalse", \verb"assertTrueMsg",\\ \verb"assertReachable", \verb"assertReachableBnd" y \verb"assertSort", y realizarán las pruebas lógicas según sus nombres, estas se explicarán más adelante con ejemplos. Veamos a continuación ejemplos de los test de unidad en que utilizaremos dichas funciones:\par

Los test deben comenzar indicando el módulo sobre el que vamos a trabajar, en nuestro caso \texttt{PEANO-NAT-EXTRA}. \par

{\codesize
\begin{verbatim}
(munit PEANO-NAT-EXTRA is
\end{verbatim}
}

El primer caso de test con que usaremos será \verb"assertEqual", que evaluará las dos expresiones que recibe como datos y devolverá \texttt{true} en el caso de que sean iguales, o \texttt{false} en el caso de que no se evalúen a lo mismo. Por ejemplo: \par

{\codesize
\begin{verbatim}
   assertEqual(s(0) + s(0), s(s(0)))
\end{verbatim}
}

De forma complementaria nos encontramos con \verb"assertDifferent", que evaluará a \texttt{true} si las expresiones evalúan a cosas distintas, y a \texttt{false} en otro caso. Por ejemplo: \par

{\codesize
\begin{verbatim}
   assertDifferent(0, s(0))
\end{verbatim}
}

Sin embargo no todas las operaciones se evalúan a una expresión, sino que es muy común que lo hagan a un Booleano. Debido a esto utilizaremos también otras dos funciones,\\ \verb"assertTrue" y \verb"assertFalse". Devolviéndonos la primera \texttt{true} en caso de que evalúe a \texttt{true} o \texttt{false} en otro caso. La segunda función se comporta al contrario, devolviéndonos \texttt{true} si la expresión evalúa a \texttt{false}, o \texttt{false} en caso contrario. Existe también una función que es una variante de \verb"assertTrue", \verb"assertTrueMsg", que, como su nombre indica, nos permite añadir un comentario a la función para que se vea en la ejecución. Por ejemplo: \par

{\codesize
\begin{verbatim}
   assertTrue(0 < s(0))
   assertFalse(s(0) < 0)
   assertTrueMsg(0 > 0 , "Deberia fallar")
\end{verbatim}
}

También puede darse el caso de que la información necesaria sea el tipo de una expresión en particular, para lo cual utilizaremos \verb"assertSort" que recibe como parámetros una expresión y un tipo, y devuelve \texttt{true} en el caso de sea el tipo de la expresión y \texttt{false} en el otro. Por ejemplo: \par

{\codesize
\begin{verbatim}
   assertSort(s(0), Nat)
\end{verbatim}
} 

Finalmente el módulo de test se cierran con \texttt{endu}. \par

{\codesize
\begin{verbatim}
endu)
\end{verbatim}
}

Por otro lado los módulos de sistema nos permiten usar, además de todas las anteriores, unas dos últimas funciones, que trabajan únicamente con reglas: \verb"assertReachable" y \verb"assertReachableBnd". La primera de ellas, \verb"assertReachable", recibe dos expresiones, y nos indica si a partir de la primera se puede alcanzar la segunda con cualquier número de pasos. La segunda, \verb"assertReachableBnd", nos indica lo mismo pero poniendo además un tope a los pasos, es decir, si desde primer estado se puede alcanzar el segundo en como mucho n pasos. Por ejemplo: \par

{\codesize
\begin{verbatim}
(munit VASIJAS is
   assertReachable(vasija(0, 3), vasija(3, 3))
   assertReachableBnd(vasija(0, 3), vasija(3, 3), 1)
endu)
\end{verbatim}
}

Construido el test solo queda evaluarlo en Maude, para lo cual basta con cargarlo en la consola, lo cual en el caso de \texttt{PEANO-NAT-EXTRA} nos daría lo siguiente:\par

{\codesize
\begin{verbatim}
Maude> load testNat.maude

	    Full Maude 2.7 March 10th 2015

	MUnit: a unit testing framework for Maude.
		Version 1.0(November 23rd, 2016)


6 test cases were executed.
1 failures.

assertEqual(s(0)+ s(0),s(s(0))) passed.
assertDifferent(0,s(0)) passed.
assertTrue(0 < s(0)) passed.
assertFalse(s(0)< 0) passed.
assertTrue(0 > 0) failed.
 --->  "Deberia fallar"
assertSort(s(0),Nat) passed.
\end{verbatim}
}

\section{Método de aprendizaje y MaudeKoan}

Además de la implementación de los distintos módulos y de sus test de unidad, se construirán de forma paralela unos módulos de aprendizaje, los cuales se encargarán de mostrar a través de distintos casos cómo ir definiendo los distintos tipos, sub-tipos y operadores que se utilizan en Maude.\par

Estos módulos de aprendizaje se construirán una vez finalizados el módulo y el test de unidad correspondiente. Serán similares a los módulos completos, con la salvedad de que presentarán huecos, que deberán rellenarse. Sin embargo una vez las funciones avancen, nos encontraremos con algunas que llevarán detrás unos cálculos que, aún siendo relativamente elementales, no se podrán dejar con huecos debido a su complejidad. Así pues, en estos casos los huecos aparecerán en los propios test de unidad donde se deberá demostrar que se ha comprendido correctamente el funcionamiento de las distintas funciones. \par

Sin embargo esta idea no es nueva, sino que se inspira en el proyecto ScalaKoans, que nos ayuda a aprender Scala con un método muy similar, aunque no igual, y que podemos ver para otros muchos lenguajes como puede ser Python. ScalaKoans pertenece al proyecto \href{http://scalakoans.webfactional.com/koans}{Koans}\footnote{Podemos encontrar más información aquí~\url{http://scalakoans.webfactional.com/koans}}, cuya filosofía es la de facilitar el acceso a los lenguajes de programación mediante un entorno interactivo que permita a aquellos que quieran familiarizarse con los lenguajes con relativa facilidad. Las diferencias entre nuestro proyecto y ScalaKoans son las siguientes:\par
\begin{itemize}
\item Los Koans solo tienen huecos en los tests, nosotros por el contrario los dejaremos en los módulos completos salvo algunas excepciones.
\item Los casos no serań completados desde un programa externo, sino que deberemos modificar los módulos \texttt{.maude} manualmente.
\item De manera análoga, no utilizaremos un programa externo para comprobar la corrección de nuestros programas, sino que se tendrán que ejecutar los test de unidad desde la propia consola de Maude hasta conseguir que todas las funciones los pasen.
\end{itemize}
 A continuación daremos un ejemplo de estos módulos incompletos, que no se corresponderá con \texttt{PEANO-NAT-EXTRA} o \texttt{VASIJAS}, sino que implementaremos un módulo nuevo, las listas. Al estar trabajando de nuevo con un módulo funcional este vuelve a empezar con \verb"fmod", sin embargo como queremos utilizar los naturales debemos importarlos. \par

{\codesize
\begin{verbatim}
fmod Lista is
   pr PEANO-NAT-EXTRA .
\end{verbatim}
}

Ahora definimos el módulo comenzando por los tipos, las variables, y finalmente dos constructores, \verb"mt" y \verb"__", correspondiéndose el primero con la lista vacía y el segundo con la concatenación de dos naturales. \par

{\codesize
\begin{verbatim}
   sort Lista .
   subsorts Nat < Lista .
   var M : Nat .
   var L : Lista .	

   op mt : -> Lista [ctor] .
   op __ : Lista Lista -> Lista [ctor assoc comm id: mt] .
endfm
\end{verbatim}
}

Una de las cosas que debe llamarnos la atención es \verb"id: mt" que nos proporciona un elemento neutro para la operación, en este caso \texttt{mt}, que identificaremos, como ya hemos dicho, con la lista vacía. \par

Por supuesto el módulo queda muy sencillo, así que lo completaremos con nuevas funciones como aquellas que nos indiquen si una lista es vacía o su longitud. Estas serán las operaciones que  se dejarán con huecos para que sean completadas, los cuales estarán complementadas con su test correspondiente. \par

{\codesize
\begin{verbatim}
op empty-list : Lista -> Bool .        op length-list : List -> *** .
*** Comprueba si una lista es la       *** Da la longitud de una lista.
*** lista vacia.                       eq length-list(mt) = 0 .
eq empty-list(mt) = true .             eq length-list(L M) = s(length-list(L)) .                    
eq empty-list(L M) = *** .                        
\end{verbatim}
}

Veamos entonces el test para listas: \par

\begin{verbatim}
(munit Lista is
   assertEqual(mt, mt mt)
   assertEqual(s(0), mt s(0))
   assertEqual(s(0) s(s(0)), s(s(0)) s(0))
   assertTrue(empty-list(mt))
   assertFalse(empty-list(s(0)))
   assertEqual(length-list(mt), 0)
   assertEqual(length-list(s(s(0))), s(0))
   assertDifferent(length-list(s(s(0))), s(s(0)))
endu)
\end{verbatim}

Ahora odemos ver que \verb"empty-list(s(0))" debe darnos \texttt{false}, de la misma manera que el tipo de salida de \verb"length-list" debe ser naturalmente \texttt{Nat}.\par 



